<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		actionBarVisible="false"
		initialize="initNews(event)"
		xmlns:s="library://ns.adobe.com/flex/spark"
		title="{LocalizationModel.newsLabel}">
	<fx:Declarations>
		<s:HTTPService id="service"
					   url="https://enigmatic-beyond-4135.herokuapp.com/services/denyse/news/1/5/"
					   result="service_resultHandler(event)"
					   fault="service_faultHandler(event)"
					   resultFormat="xml"/>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import assets.AssetsLibrary;

			import model.LocalizationModel;
			import model.NewsModel;

			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.core.IVisualElement;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;

			import spark.components.Application;
			import spark.layouts.VerticalLayout;

			import views.details.NewsDetailsView;
			import views.renderers.NewsItemRenderer;

			import vo.VoNews;

			private var pressed:Boolean = false;
			private var callingService:Boolean = false;
			private var startPointPosition:Number = 0;
			private var lastIndexInViewWhenPressed:Number = 0;

			/* private static var instance:NewsView;

			public static function getInstance():NewsView
			{
				if (instance == null)
				{
					instance = new NewsView();
				}

				return instance;
			} */

			private function initNews(event:FlexEvent):void
			{
				/* var file:File = File.applicationDirectory.resolvePath("assets/xml/news.xml");
				var stream:FileStream = new FileStream();
				var xml:XML;
				stream.open(file, FileMode.READ);
				xml = XML(stream.readUTFBytes(stream.bytesAvailable));
				stream.close();
				loadModel(xml); */
				if (NewsModel.arrNewsItems.length == 0)
					service.send();
			}

			private function loadModel(xml:XML):void
			{
				for each (var newsItem:XML in xml.news)
				{
					var news:VoNews = new VoNews();
					news.id = newsItem.id;
					news.title = newsItem.title;
					news.body = newsItem.body;
					news.date = newsItem.date;
					news.photoURL = newsItem.photoURL;
					NewsModel.arrNewsItems.addItem(news);
					NewsModel.newsPublished++;
				}

				idList.dataProvider = NewsModel.arrNewsItems;
			}

			protected function idList_mouseDownHandler(event:MouseEvent):void
			{
				pressed = true;
				var vLay:VerticalLayout = idList.layout as VerticalLayout;
				lastIndexInViewWhenPressed = vLay.lastIndexInView;
				startPointPosition = this.mouseY;
			}

			protected function idList_mouseUpHandler(event:MouseEvent):void
			{
				pressed = false;

				if (callingService)
					callingService = false;
			}

			protected function idList_mouseMoveHandler(event:MouseEvent):void
			{
				if (pressed)
				{
					var menuBar:Number = event.stageY - this.mouseY;
					var firstRow:IVisualElement = idList.dataGroup.getElementAt(0);

					if (firstRow)
					{
						var rowHeight:Number = NewsItemRenderer(firstRow).measuredHeight;
						var numberOfRowsInView:Number = Math.ceil((this.screen.height - menuBar) / rowHeight) - 1;
						var vLay:VerticalLayout = idList.layout as VerticalLayout;
						var appDPI:Number = FlexGlobals.topLevelApplication.applicationDPI;
						var trigger:Number = 0;
						var extraOffset:Number = 0;

						if (lastIndexInViewWhenPressed > vLay.lastIndexInView)
						{
							extraOffset = (lastIndexInViewWhenPressed - vLay.lastIndexInView) * rowHeight;
						}

						switch (appDPI)
						{
							case 160:
								trigger = 100;
								break;
							case 240:
								trigger = 175;
								break;
							case 320:
								trigger = 350;
								break;
							default:
								break;
						}

						if ((numberOfRowsInView == vLay.lastIndexInView) && (this.mouseY - startPointPosition > (trigger + extraOffset)) && !callingService)
						{
							trace("Dispara evento NOTICIAS!!!");
							callingService = true;
							//service.url = "https://enigmatic-beyond-4135.herokuapp.com/services/denyse/news/" + (NewsModel.newsPublished + 1) + "/5/";
							service.url = "https://enigmatic-beyond-4135.herokuapp.com/services/denyse/news/1/5/";
							service.send();
						}
					}
				}
			}

			protected function service_resultHandler(event:ResultEvent):void
			{
				trace("Ã‰xito en XML News!!!");
				var resultString:String = String(event.result);
				loadModel(new XML(resultString));
			}

			protected function service_faultHandler(event:FaultEvent):void
			{
				trace("Fracaso en XML Noticias!!!");
			}
		]]>
	</fx:Script>
	<s:BitmapImage source="{AssetsLibrary.generalBackground}"
				   left="0"
				   alpha="0.6"
				   right="0"
				   width="100%"
				   height="100%"/>
	<s:VGroup height="100%"
			  width="100%"
			  gap="0">
		<s:List id="idList"
				mouseDown="idList_mouseDownHandler(event)"
				mouseUp="idList_mouseUpHandler(event)"
				mouseMove="idList_mouseMoveHandler(event)"
				contentBackgroundAlpha="0.0"
				height="100%"
				width="100%"
				bottom="0"
				left="0"
				right="0"
				change="navigator.pushView(NewsDetailsView, idList.selectedItem)"
				itemRenderer="views.renderers.NewsItemRenderer">
		</s:List>
		<!--<s:Label id="lblMoreItems"
				 width="100%"
				 height="15"
				 styleName="descriptionStyleEmbed"
				 text="{LocalizationModel.moreItemsLabel}"/>-->
	</s:VGroup>
</s:View>
