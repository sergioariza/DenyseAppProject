<?xml version="1.0" encoding="utf-8"?>
<s:IconItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
					xmlns:s="library://ns.adobe.com/flex/spark"
					labelField="title"
					styleName="titleStyleNotEmbed"
					iconField="photoURL"
					textAlign="center"
					cacheAsBitmap="true"
					messageStyleName="descriptionStyleNotEmbed"
					messageField="body">
	<fx:Script>
		<![CDATA[
			import mx.graphics.SolidColor;
			import mx.graphics.SolidColorStroke;
			import mx.graphics.Stroke;
			import mx.states.SetEventHandler;

			import spark.components.Group;
			import spark.primitives.Rect;
			import spark.primitives.supportClasses.FilledElement;

			import vo.VoNews;

			private var groupIcon:Group;

			private var voNews:VoNews;

			override public function set data(value:Object):void
			{
				super.data = value;

				if (data == null)
					return;

				voNews = new VoNews(value);
			}

			override protected function createChildren():void
			{
				super.createChildren();

				groupIcon = new Group();
				var rect:Rect = new Rect();
				rect.alpha = 0.3;
				rect.top = 0;
				rect.bottom = 0;
				rect.left = 0;
				rect.right = 0;
				rect.stroke = new SolidColorStroke(0x000000, 1, 0.3)
				rect.fill = new SolidColor(0x000000);
				groupIcon.addElement(rect);
				addChild(groupIcon);
			}

			override protected function measure():void
			{
				super.measure();
				formatMessageFieldText();

				//Resize measuredHeight after formatting messageField and resize icon
				iconHeight = this.screen.width * 0.25;
				iconWidth = this.screen.width * 0.25;

				var marginTopBottom:Number = iconHeight * 0.15;
				measuredHeight = iconHeight + (marginTopBottom * 2);
			}

			private function formatMessageFieldText(numLinesParam:int = 3):void
			{
				var messageText:String = voNews.body;
				messageDisplay.text = messageText;
				var enoughTrim:Boolean = false;

				while (!enoughTrim)
				{
					if (messageDisplay.numLines > numLinesParam)
					{
						messageText = messageText.substr(0, messageText.length - 1);
						messageDisplay.text = messageText;
					}
					else
					{
						enoughTrim = true;
					}
				}

				enoughTrim = false;
				messageDisplay.text = messageDisplay.text.substr(0, messageDisplay.text.length - 3);

				while (!enoughTrim)
				{
					if (messageDisplay.text.charAt(messageDisplay.text.length - 1) == ' ')
					{
						messageDisplay.text = messageDisplay.text.substr(0, messageDisplay.text.length - 1);
						enoughTrim = true;
					}
					else
					{
						messageDisplay.text = messageDisplay.text.substr(0, messageDisplay.text.length - 1);
					}
				}

				messageDisplay.text = messageDisplay.text + "...";
			}

			override protected function layoutContents(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.layoutContents(unscaledWidth, unscaledHeight);
				var groupWidth:Number = iconDisplay.width + 10;
				var groupHeight:Number = iconDisplay.height + 10;
				setElementSize(groupIcon, groupWidth, groupHeight);

				if (!groupIcon.containsElement(iconDisplay))
				{
					groupIcon.addElement(iconDisplay);
				}

				var topGroup:Number = Math.round(((unscaledHeight - groupHeight) / 2));
				setElementPosition(groupIcon, 5, topGroup);
				setElementPosition(iconDisplay, 5, 5);
			}

			override protected function drawBackground(unscaledWidth:Number, unscaledHeight:Number):void
			{
				//Transparent background for hit detection
				graphics.beginFill(0x000000, 0.15);
				graphics.lineStyle();
				graphics.drawRect(0, 0, unscaledWidth, unscaledHeight);
				graphics.endFill();

				graphics.moveTo(0, 0);
				graphics.lineStyle(1, 0x484848, 0.3);
				graphics.lineTo(unscaledWidth, 0);

				graphics.moveTo(0, unscaledHeight - 1);
				graphics.lineTo(unscaledWidth, unscaledHeight - 1);

				//Turn off opaqueBackground since this renderer has some transparency
				opaqueBackground = null;
			}
		]]>
	</fx:Script>
</s:IconItemRenderer>
